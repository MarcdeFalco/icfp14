INT 3
MOV H,A ; H - ghost number
INT 5 ; Get current position
MOV C,A ; C - Current X
MOV D,B ; D - Current Y
INT 3
INT 6 ; Get current status
MOV G,B ; G - Current direction
JEQ 34,A,2 ; If the ghost is invisible then run towards LambdaMan
JGT 34,[H],20 ; If the last 20 steps were on empty squares (without pills) then run towards LambdaMan
JEQ 22,G,0
MOV A,C
MOV B,D
INC B ; Try to go down
INT 7 ; Get the content of down square
JEQ 22,A,0 ; If we can't go down then we try to go left
JEQ 18,A,1 ; If it's empty then we increase the consecutive empty squares counter
MOV [H],255 ; Reset the consecutive empty squares counter
INC [H]
MOV A,2
INT 0 ; Go down
HLT
JEQ 69,G,1
MOV A,C
MOV B,D
DEC A ; Try to go left
INT 7 ; Get the content of left square
JEQ 33,A,0 ; If we can't go left then halt
JEQ 30,A,1 ; If it's empty then we increase the counter
MOV [H],255 ; Reset the counter
INC [H]
MOV A,3
INT 0 ; Go left
HLT
INT 1
MOV E,A ; E - LambdaMan X
MOV F,B ; F - LambdaMan Y
JEQ 57,C,E ; If they're equal we go up or down
JGT 48,C,E ; If we must go left 
JEQ 57,G,3 ; If we can't turn
MOV A,C
MOV B,D
INC A ; We try to go right
INT 7
JEQ 57,A,0 ; If we can't go right
MOV A,1
INT 0 ; Go right
HLT
JEQ 57,G,1 ; If we can't turn
MOV A,C
MOV B,D
DEC A ; We try to go left
INT 7
JEQ 57,A,0 ; If we can't go left
MOV A,3
INT 0 ; Go left
HLT
JEQ 76,D,F ; If they're equal then halt
JGT 68,D,F ; If we must go up
JEQ 76,G,0 ; If we can't turn
MOV A,C
MOV B,D
INC B ; We try to go down
INT 7
JEQ 76,A,0 ; If we can't go down
MOV A,2
INT 0 ; Go down
HLT
JEQ 76,G,2 ; If we can't turn
MOV A,C
MOV B,D
DEC B ; We try to go up
INT 7
JEQ 76,A,0 ; If we can't go up
MOV A,0
INT 0 ; Go up
HLT
